#using PluginsToExcelTGG.*
#using AttrCondDefLibrary.*

#rule ExtensionNodeToNewRowUnderBlockHeaderRelation #with PluginsToExcelTGG

#source {
	
	xmlFolder_workspace:Folder{
		-subFolder -> pluginFolder
		-subFolder -> pluginFolder1
	}

	pluginFolder:Folder {
		-file -> pluginXmlFile
	}
	
	pluginFolder1:Folder {
		-file -> pluginXmlFile1
	}

	pluginXmlFile:File {
		name=="plugin.xml"
		-rootNode -> pluginProviderNode
	}
	
	pluginXmlFile1:File {
		name=="plugin.xml"
		-rootNode -> pluginNode
	}

	pluginNode:Node {
		++-children -> extensionNode
	}

	pluginProviderNode:Node {
		-children -> extensionPointNode
	}
	
	extensionPointNode:Node{
		-attribute -> extensionPointAttr
	}
	
	extensionPointAttr: Attribute{
		name  == "id"
	} 
	
	++extensionNode:Node {
		name:="extension"
		++-attribute -> extensionAttr
	}

	++extensionAttr:Attribute{
		name:="point"
	}
}

#target {
	// create new cells for extension points
	++cell_1:Cell
	/*{
		text:=""
	} */

	++cell_2:Cell

	++cell_3:Cell

	++cell_4:Cell
	
		// create new row for extension
	++newRowForExtenstion:Row{
		++-cell -> cell_1
		++-cell -> cell_2
		++-cell -> cell_3
		++-cell -> cell_4
	}

		// add new cells to existing cols
	componentsColumn:Column{
		++-cell -> cell_1
		-nextColumn -> dependenciesColumn
	}

	dependenciesColumn:Column{
		++-cell -> cell_2
		-nextColumn -> documentationColumn
	}

	documentationColumn:Column{
		++-cell -> cell_3
		-nextColumn -> reviewColumn
	}

	reviewColumn:Column{
		++-cell -> cell_4
	}

		// adding new row after block header
	blockHeader:Row{
		++-nextRow -> newRowForExtenstion
	}

	sheet:Sheet {
		++-rowobject -> newRowForExtenstion
		-rowobject -> blockHeader
		++-cell -> cell_1
		++-cell -> cell_2
		++-cell -> cell_3
		++-cell -> cell_4
		-colobject -> componentsColumn
		-colobject -> dependenciesColumn
		-colobject -> documentationColumn
		-colobject -> reviewColumn
	}
}

#correspondence {

	pluginFolderToBlockHeaderRowRule:PluginNodeToRowRelation{
		#src->pluginNode
		#trg->blockHeader
	}

	++extensionNodeToRowRule:ExtensionNodeToRowRelation{
		#src->extensionNode
		#trg->newRowForExtenstion
	}
}

#attributeConditions {
	concat(".", pluginFolder.name, extensionPointAttr.value, extensionAttr.value)
	//eq_string(extensionPointAttr.value, extensionAttr.value)
	eq_string(extensionAttr.value, cell_2.text)
}



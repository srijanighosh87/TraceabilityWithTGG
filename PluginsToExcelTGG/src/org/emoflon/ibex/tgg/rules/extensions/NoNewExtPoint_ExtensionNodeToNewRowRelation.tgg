#using PluginsToExcelTGG.*
#using AttrCondDefLibrary.*

#rule NoNewExtPoint_ExtensionNodeToNewRowRelation #with PluginsToExcelTGG

#source {

	xmlFolder_workspace:Folder{
		-subFolder -> newpluginFolder
		-subFolder -> pluginFolder1
	}

	newpluginFolder:Folder {
		-file -> newpluginXmlFile
	}
	
	pluginFolder1:Folder {
		-file -> pluginXmlFile1
	}

	newpluginXmlFile:File {
		name=="plugin.xml"
		-rootNode -> newpluginProviderNode
	}
	
	pluginXmlFile1:File {
		name=="plugin.xml"
		-rootNode -> pluginNode
	}
	
		
	newpluginProviderNode:Node {
		name=="plugin"
		-children -> newextensionPointNode
	}
	
	newextensionPointNode:Node{
		name=="extension-point"
		-attribute -> newextensionPointAttr
	}
	
	newextensionPointAttr: Attribute{
		name == "id"
	} 
	
	extensionNodeOld:Node{
	}
	
	pluginNode:Node {
		-children->extensionNodeOld
		++-children -> extensionNode
	}

	
	++extensionNode:Node {
		name:="extension"
		++-attribute -> extensionAttr
	}

	++extensionAttr:Attribute{
		name:="point"
	}

}

#target {
	// create new cells for extension points
	++cell_1:Cell

	++cell_2:Cell

	++cell_3:Cell

	++cell_4:Cell

		// create new row for extension
	++newRowForExtenstion:Row{
		++-cell -> cell_1
		++-cell -> cell_2
		++-cell -> cell_3
		++-cell -> cell_4
	}

		// add new cells to existing cols
	componentsColumn:Column{
		++-cell -> cell_1
		-nextColumn -> dependenciesColumn
	}

	dependenciesColumn:Column{
		++-cell -> cell_2
		-nextColumn -> documentationColumn
	}

	documentationColumn:Column{
		++-cell -> cell_3
		-nextColumn -> reviewColumn
	}

	reviewColumn:Column{
		++-cell -> cell_4
	}
	
	lastRow:Row{
		++-nextRow->newRowForExtenstion
	}
	
	
	sheet:Sheet {
		++-rowobject -> newRowForExtenstion
		-rowobject->lastRow
		
		++-cell -> cell_1
		++-cell -> cell_2
		++-cell -> cell_3
		++-cell -> cell_4
		-colobject -> componentsColumn
		-colobject -> dependenciesColumn
		-colobject -> documentationColumn
		-colobject -> reviewColumn
		
	}

}

#correspondence {
	++extensionNodeToRowRule:ExtensionNodeToRowRelation{
		#src->extensionNode
		#trg->newRowForExtenstion
	}
	
	extensionNodeToOldRowRule:ExtensionNodeToRowRelation{
		#src->extensionNodeOld
		#trg->lastRow
	}
	
}

#attributeConditions {
	concat(".", newpluginFolder.name, newextensionPointAttr.value, extensionAttr.value)
	eq_string(extensionAttr.value, cell_2.text)
}
